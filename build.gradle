plugins {
	id 'eclipse'
	id 'fabric-loom' version '1.8.12'
	id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.9'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	mavenCentral()
	flatDir { dirs "gradle\\deps" }
	maven { url "https://jitpack.io/" }
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://api.modrinth.com/maven" }
	maven { url "https://maven.dragons.plus/releases" }
	maven { url "https://maven.terraformersmc.com/releases/" }
	maven { url "https://oss.sonatype.org/content/groups/public/" }
	maven { url "https://repo.papermc.io/repository/maven-public/" }
	maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
}

loom {
	splitEnvironmentSourceSets()
	accessWidenerPath = file("src/main/resources/xdlib.accesswidener")

	mods {
		"xdlib" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
}

fabricApi {
	configureDataGeneration()
}

dependencies {
	minecraft("com.mojang:minecraft:1.21")
	mappings("net.fabricmc:yarn:1.21+build.9:v2")
	compileOnly("io.papermc.paper:paper-api:1.21.3-R0.1-SNAPSHOT")
	compileOnly("net.md-5:bungeecord-api:1.21-R0.1-SNAPSHOT")
	compileOnly("org.spigotmc:spigot-api:1.21.3-R0.1-SNAPSHOT")
	compileOnly("com.velocitypowered:velocity-api:3.4.0-SNAPSHOT")
	annotationProcessor("com.velocitypowered:velocity-api:3.4.0-SNAPSHOT")
	include(implementation("dev.xdpxi:dependencies:1.1.7"))

	modImplementation("net.fabricmc:fabric-loader:0.16.9")
	modImplementation("net.fabricmc.fabric-api:fabric-api:0.107.3+")
	modImplementation("com.terraformersmc:modmenu:11.0.3")
	modImplementation("me.shedaniel.cloth:cloth-config-fabric:16.0.141"){exclude(group: "net.fabricmc.fabric-api")}
	modImplementation("maven.modrinth:ducky-updater-lib:2024.10.2")
	implementation("club.minnced:discord-rpc-release:v3.4.0")
	implementation("com.github.MinnDevelopment:java-discord-rpc:2.0.2")
	implementation("org.tomlj:tomlj:1.1.1")
	implementation("org.antlr:antlr4-runtime:4.13.2")
	implementation("org.json:json:20240303")
	implementation("com.formdev:flatlaf:3.5.2")
	implementation("org.commonmark:commonmark:0.24.0")
	implementation("org.projectlombok:lombok:1.18.34")
	implementation("org.beanshell:bsh:3.0.0-SNAPSHOT")
	implementation("io.hotmoka:toml4j:0.7.3")
	implementation("org.yaml:snakeyaml:2.3")
}

processResources {
	def props = [version: version]
	inputs.properties props
	filteringCharset 'UTF-8'
	filesMatching('paper-plugin.yml') {
		expand props
	}
	filesMatching('bungee.yml') {
		expand props
	}

	inputs.property "version", project.version
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')
def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
	def props = ['version': project.version]
	task.inputs.properties props

	task.from templateSource
	task.into templateDest
	task.expand props
}

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })
project.idea.project.settings.taskTriggers.afterSync generateTemplates
project.eclipse.synchronizationTasks(generateTemplates)
